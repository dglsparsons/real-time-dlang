%
%
%
This chapter aims to describe the implementation of the project requirements into the D
programming language, highlighting the completed implementations and any  
decisions made during their design.

\section{Method of Provision}
For the end goal of this project to be usable, various methods for its delivery 
were considered. There are two significant alternatives. 
If the compiler or the 
core runtime features of the language do not need to be altered to provide the 
desired functionality, the project may be packaged as an external library. 
This is the most desirable end goal, as it succeeds in meeting requirement 8. 
Failing this, a modified runtime and/or compiler would need be provided.
However, this system would require maintenance to keep it up to date with the
core set of D functionality. 

\section{Requirement 1: Concurrency}
D, being a C/C++ style language, provides support for the creation and use of 
threads. Threads in D follow a Java style approach: \texttt{Thread} is a class, 
allowing subclasses to be derived. The default 
class method will allow a function to be passed by reference and executed. 
These features can be seen in the example below, taken from the D website
\cite{core-thread}: 
%TCusedtoignorethis
\begin{lstlisting}[basicstyle=\small]
import core.Thread; 

class DerivedThread : Thread
{
    this()
    {
        super(&run);
    }
    private:
    void run()
    {
        // Derived thread running.
    }
}

void threadFunc()
{
    // Composed thread running.
}

// create and start instances of each type
void main()
{
    auto derived = new DerivedThread();
    auto composed = new Thread(&threadFunc);
    derived.start; 
    composed.start;
}
\end{lstlisting}
%TCusedtoendignorethis
It is therefore evident that D provides 
concurrent control of system components. Therefore, for requirement 1, no
further implementation is required. 

\section{Requirement 2: Priority Scheduling}
The ability to correctly schedule tasks or threads based on their priority is of 
critical importance to a real-time system. D's \texttt{Thread} class contains the
parameter, \texttt{priority}, which can be used to adjust or retrieve 
the \texttt{Thread's} priority \cite{core-thread}. 
However, in order for priority based scheduling to be used, this alone is not 
enough: the system scheduler must also be changed.
No support exists within D's standard libraries to set the scheduler, and therefore, 
this support must be supplied. 
This is achieved through interaction with standard C libraries. 
The Linux function \texttt{sched\textunderscore{}setscheduler} allows the operating system 
scheduler to be changed, along with the scheduler's priority, to either a 
first-in-first-out or round robin scheduler \cite{sched-setscheduler}. 
In order to provide an intuitive interaction, a wrapper has been written around
this system call, providing a more 'D like' interface and a level of
abstraction. This takes the following form:
\begin{lstlisting}[basicstyle=\small,language=C++]
public import core.sys.posix.sched 
                : SCHED_FIFO, SCHED_OTHER, SCHED_RR; 

void setScheduler(int scheduler_type, int scheduler_priority)
{
    import core.sys.posix.sched 
                : sched_param, sched_setscheduler; 

    sched_param sp = { 
        sched_priority: scheduler_priority 
    }; 

    int ret = sched_setscheduler(0, scheduler_type, &sp); 
    if (ret == -1) {
        throw new Exception("scheduler did not properly set");
    }
}

void setFIFOScheduler(int schedPriority)
{
    setScheduler(SCHED_FIFO, schedPriority);
}

void setRRScheduler(int schedPriority)
{
    setScheduler(SCHED_RR, schedPriority); 
}
\end{lstlisting}
This functionality may then be used in the following manner: 
\begin{lstlisting}[basicstyle=\small]
void main()
{
    setFIFOScheduler(50); 
}
\end{lstlisting}
The ability to set the scheduler to use a priority based method, combined with 
D's ability to set the priority of a \texttt{Thread}, enables a 
priority based scheduling approach to be used. 
For some operating systems, setting the scheduler may require super user privileges. 
Furthermore, this project is interested only in operating systems capable of meeting tight timing 
requirements in any calls to the kernel and context switching.
If using Linux, the underlying operating system may require patching in order 
to be fully viable for a real-time system. Because of this, real-time patches 
to the Linux kernel exist \cite{rt-wiki}. 
Furthermore, special care must also be taken when developing such a system;
selecting appropriate hardware and programming the system in a manner appreciative 
of the underlying hardware \cite{rt-wiki-how-to}.


\section{Requirements 3 and 4: Monotonic Clocks and Absolute Sleep}
As mentioned in the previous section, it is a necessary to be able to sleep until 
an absolute time. This sleep must also use a monotonic clock 
to remove any subjection to clock-drift or time-zone changes. D has support 
for a monotonic clock as part of its core libraries 
\cite{dlang-core-time}. This is accessible using 
\texttt{MonoTime}. However, there is no ability to sleep until an 
absolute time. Using D's ability to interact with C libraries, it is possible 
to leverage C's ability to perform this operation. The C function 
\texttt{clock\textunderscore{}nanosleep} allows an absolute time to be specified
\cite{clock-nanosleep}.
In order to provide a more usable interaction, this project provides a wrapper
function. 
This function accepts the D type \texttt{MonoTime} as an input, converts it to the C 
equivalent, and calls the C function \texttt{clock\textunderscore{}nanosleep}, 
allowing an absolute delay. The code is displayed below: 
\begin{lstlisting}[basicstyle=\small]
void delayUntil(MonoTime timeIn)
{
    import core.sys.linux.time; 
    import core.time : Duration, timespec; 
    Duration dur = timeIn - MonoTime(0) ;
    long secs, nansecs; 
    dur.split!("seconds", "nsecs")(secs, nansecs); 
    timespec sleep_time = timespec(secs, nansecs); 
    if (clock_nanosleep(CLOCK_MONOTONIC, TIMER_ABSTIME, 
                                    &sleep_time, null)) {
        throw new Exception("Failed to sleep as expected!"); 
    }
}
\end{lstlisting}
This allows simplistic use for a real-time application. An absolute sleep can 
then be performed in the following manner: 
\begin{lstlisting}[basicstyle=\small]
void main()
{
    auto time = MonoTime.currTime; 
    time += 3.seconds; 
    delayUntil(time);
}
\end{lstlisting}
As such, this project provides support for pausing a \texttt{Thread's}
execution until an absolute time. 

\section{Requirements 5 and 6: Bounded Priority Inversion}
In D, the \texttt{Mutex} class follows a similar approach to the \texttt{Thread}
class: the language's implementation provides a wrapper around operating system 
calls. However, this \texttt{Mutex} class has no 
inherent ability to provide the Priority Inheritance or the Priority Ceiling protocol. 
Real-time systems have been shown to only be viable on operating systems that 
are POSIX compliant, and due to the quantity of work involved in reimplementing 
and defining these protocols, the decision was made to expand the languages
inbuilt POSIX mutexes. The priority inheritance and priority ceiling protocols 
are available when using POSIX mutexes in C \cite{mutex-setprotocol}. 
\par\bigskip\noindent
As requirement 8 highlights the importance of an unmodified runtime, the 
constructor for \texttt{Mutex} class cannot be extended. Instead it must be fully 
reimplemented as a new class with the constructor adding 
the C function \texttt{pthread\textunderscore{}mutexattr\textunderscore{}setprotocol} 
\cite{mutex-setprotocol}. 
The implementation of this constructor for the \texttt{RTMutex} class is defined:
\lstinputlisting[basicstyle=\small,language=Java]{rtmutex.d}
The full implementation of \texttt{RTMutex} can be seen in Appendix A.1. 
A mutex using the inheritance protocol may then be created and initialised in 
the following manner: 
\begin{lstlisting}[basicstyle=\small]
auto a = new RTMutex(PRIORITY_INHERIT);
\end{lstlisting}
However, this is not a very D-like implementation: \texttt{enums} are not widely used 
in constructors. Instead, as with the scheduler calls, 
it is more intuitive to wrap this into two separate classes. Additionally, 
further support 
is needed to set and retrieve the priority ceiling associated with the 
mutex. 
This is achieved by a second class that provides access to an \texttt{RTMutex}
instance alongside a wrapper around the C functions 
\texttt{pthread\textunderscore{}mutex\textunderscore{}getprioceiling} and 
\texttt{pthread\textunderscore{}mutex\textunderscore{}setprioceiling}.
This is implemented in the following manner: 
\begin{lstlisting}[basicstyle=\small]
class CeilingMutex 
{
    private import core.sync.exception : SyncError;
    alias ceilingMutex this;
    RTMutex ceilingMutex;

    // Initializes a new CeilingMutex
    this()
    {
        ceilingMutex = new RTMutex(PROTOCOL_CEILING);
        this.ceiling = 1;
    }

    final @property int ceiling()
    {
        int ceiling; 
        if(pthread_mutex_getprioceiling(this.handleAddr, 
                                                &ceiling))
            throw new SyncError("Unable to fetch the priority 
                           ceiling."); 
        return ceiling; 
    }

    final @property void ceiling(int val)
    {
        if(pthread_mutex_setprioceiling(this.handleAddr, 
                                                val, null))
            throw new SyncError("Unable to set the priority 
                           ceiling."); 
    }
}
\end{lstlisting}
This allows the priority ceiling to be set or retrieved as if it were a property of 
the \texttt{RTMutex} class. Similarly, the functions defined in \texttt{RTMutex} 
can be accessed as though they were part of the \texttt{CeilingMutex} class. 
A similar result is achieved for the \texttt{InheritanceMutex} class.
This allows an idiomatic and readable interaction: 
\begin{lstlisting}[basicstyle=\small]
auto a = new CeilingMutex;
a.ceiling = 50; 
synchronized(a) 
{
    // perform an exclusive action
}
\end{lstlisting}
Therefore, on a POSIX compliant operating system, D's \texttt{Mutex} class can 
be reimplemented, incorporating both the priority inheritance and 
priority ceiling protocols. 

\section{Requirement 7: Asynchronous Transfer of Control} % 600 words
The final primitive required for a real-time system is the ability to perform an Asynchronous Transfer of 
Control (ATC). This entails providing a section of code that can be interrupted 
and aborted asynchronously.
As detailed in the previous section, there are several different methods of 
achieving this aim. Here, each method is considered and the logic behind the 
implementation explained. 

\subsubsection*{Setjmp and Longjmp} 
In C, ATC can be achieved through use of the functions, \texttt{setjmp} and 
\texttt{longjmp}. These allow 
the current execution of a program to be altered by `jumping' to a saved
point in the execution.
This jump may occur asynchronously through the use of POSIX signals 
and a signal handler. 
However, when using this approach, even though the functions are thread and
signal safe \cite{setjmp}, it is possible for the stack to become corrupted in
D, as with C++ \cite{unwinding-stack}. 
During ATC, the stack may be altered as functions are called, and returning to a 
previous location without correctly rewinding the stack may cause memory corruption. 
As a result of this poor memory management, this method was 
not implemented in the end product.

\subsubsection*{Thread Cancellation}
The second approach considered for the implementation of ATC in D is a thread 
cancellation approach. When using POSIX threads, it is possible to terminate a 
thread during its execution using the \texttt{pthread\textunderscore{}cancel} function. 
In order to neatly abstract this cancellation, an \texttt{Interruptible} class
was defined.
Initialising this will create a new \texttt{Thread}, 
inheriting the priority of the calling \texttt{Thread}. 
On calling a \texttt{start} function, an abortable section of code will begin 
execution in this \texttt{Thread}, and the calling
\texttt{Thread} will become blocked. 
For the asynchronous interrupt mechanism, using
\texttt{pthread\textunderscore{}cancel},
two alternative approaches were considered:
the first follows the Java implementation of a 
\texttt{Thread.interrupt} method; 
the second approach instead places this \texttt{interrupt} method as part of the 
\texttt{Interruptible} class. 
While the difference between the two approaches may appear small, it is significant: 
in the first implementation, it is not clear which \texttt{Interruptible}
section is to be cancelled in a situation where the \texttt{Interruptible} 
sections are nested. 
Due to this ambiguity, a decision was made to instead implement the second
approach. 
For example, this can be used to provide a 2 second timeout on a function call:
\begin{lstlisting}[basicstyle=\small]
void interruptibleSection()
{
    while(true)
    {
        // loop forever.
    }
}

void main()
{
    auto a = new Interruptible(&interruptibleSection); 
    new Thread({
        Thread.sleep(2.seconds);
        a.interrupt;
    }).start;
    a.start; 
}
\end{lstlisting}
\par\bigskip\noindent
So far, it has been assumed that the interrupt function performs the exact 
functionality required. 
However, using \texttt{pthread\textunderscore{}cancel} alone does not guarantee 
an immediate cancellation: it is only guaranteed 
to cancel once both a signal is received and a cancellation point in the code 
is reached. 
Many standard C functions are defined as being thread cancellation points, but 
there is no guarantee that these may be called within the target
\texttt{Thread} 
\cite{pthread-cancel-points}. It is possible to set the 
cancellation of a \texttt{Thread} to be immediate through the function 
\texttt{pthread\textunderscore{}setcanceltype}, by setting the value to 
\texttt{PTHREAD\textunderscore{}CANCEL\textunderscore{}ASYNCHRONOUS}. This is 
crucial in either tight loops or where cancellation must be immediate. 
\par\bigskip\noindent
However, this immediate cancellation comes at the cost of safety: interrupting a 
\texttt{Thread} during the middle of a function call, such as a memory allocation, 
may leave memory in an inconsistent state or crash the program. 
This gives rise to the requirement of having regions of code in which
interrupts are deferred. 
Two alternative approaches were considered for the implementation 
of this feature. First, a method of the 
\texttt{Interruptible} class could toggle the cancellation of a
\texttt{Thread} between \texttt{PTHREAD\textunderscore{}CANCEL\textunderscore{}ENABLE}
and \texttt{PTHREAD\textunderscore{}CANCEL\textunderscore{}DISABLE}, or,
secondly,
a boolean flag could be toggled in the \texttt{Interruptible} class indicating
whether it is possible to interrupt. 
Using either approach, any interrupt that arrives while interrupts are deferred
must be stored. When interrupts are re-enabled, stored interrupts are then
executed. 
While the two approaches appear similar, the first approach does 
not provide a viable method for performing nested cancellations: setting 
\texttt{PTHREAD\textunderscore{}CANCEL\textunderscore{}DISABLE} may cause a
nested inner section not to be cancelled, even when its outer parent  is cancelled. 
In order to provide a fine-grained control over cancellation, 
even when nesting, both these methods are implemented. 
Basic deferral can be achieved through the \texttt{deferred} properties which
use the second method. However, in order 
to execute code safely, even in nested cases, an \texttt{executeSafely} method that 
defers the cancellation has been implemented as follows. 
\begin{lstlisting}[basicstyle=\small]
class Interruptible
{
    ..
    void executeSafely(void delegate() fn)
    {
        if (pthread_setcancelstate(PTHREAD_CANCEL_DISABLE, 
                                                    null))
        {
            throw new Error("Unable to set thread cancellation 
                                                    state");
        }
        fn();
        if (pthread_setcancelstate(PTHREAD_CANCEL_ENABLE, 
                                                      null))
        {
            throw new Error("Unable to set thread cancellation 
                                                    state");
        }
    }
}
\end{lstlisting}
this may then be used in the following manner. 
\begin{lstlisting}[basicstyle=\small]
alias getInt = Interruptible.getThis;
void interruptibleFunction() 
{
    for(int i = 0; i < 10; i++)
    {
        void update() 
        {
            void* x = GC.malloc(10_000); 
        }
        getInt.executeSafely(&update);
    }
}
\end{lstlisting}
This provides a safe mechanism for handling memory allocation or critical sections 
of code. Furthermore, this enables code to be executed safely despite being 
nested in outer \texttt{Interruptible} classes. 
\par\bigskip\noindent
Additionally, it may be desirable to execute cleanup code after cancellation 
has occurred. This is achieved through the use of the 
\texttt{pthread\textunderscore{}cleanup\textunderscore{}push} function. 
Wrappers are provided around the C function, providing a usable interface. An 
example of using this cleanup code is as follows: 
\begin{lstlisting}[basicstyle=\small]
extern (C) void thread_cleanup(void* arg) nothrow
{
    // cleanup function
}

void interruptibleFn()
{
    auto a = addCleanup(&thread_cleanup, cast(void*)void);
    // perform some work
}
\end{lstlisting}
This provides the desired functionality of ATC, as set out in requirement 7, 
through the use of thread cancellation in a two-thread model.

\subsubsection*{Exceptions and Signals}
An alternative approach to the implementation of ATC is to use D's inbuilt 
\texttt{Exception} and \texttt{Error} handling mechanisms, combined with
signals. 
For a POSIX system, it is possible to asynchronously invoke code using 
signals and signal handlers. Furthermore, by using a
real-time signal, such as SIGRTMIN, it is guaranteed that signals
arrive in a timely manner and that they arrive in order. 
While typically used on a process wide level, it
is also possible to invoke signal handlers for a specific \texttt{Thread} through
use of the C function, \texttt{pthread\textunderscore{}kill}. 
As \texttt{pthread\textunderscore{}kill} executes the signal handler in the
context of the target \texttt{Thread} \cite{pthread-kill}, an \texttt{Exception} thrown will
propagate on the target \texttt{Thread's} stack, enabling it to be caught. 
The following example shows a simple invocation of ATC achieved by throwing and 
catching an \texttt{Exception}:
\begin{lstlisting}[basicstyle=\small]
import core.sys.posix.pthread, 
       core.sys.posix.signal, 
       std.stdio;

Exception ex = new Exception("Remotely Triggered Exception"); 

extern (C) void sig_handler(int signum) @nogc nothrow
{
    throw ex;
}

void setupSignalHandler()
{
    sigaction_t action; 
    action.sa_handler = &sig_handler; 
    sigemptyset(&action.sa_mask);
    sigaction(36, &action, null); 
}

void threadFunction()
{
    writeln("This is the thread"); 
    while(true)
    {
        Thread.sleep(1.seconds);
    }
}

void main()
{
    setupSignalHandler; 

    auto a = new Thread(&threadFunction); 
    a.start; 

    Thread.sleep(1.seconds); 
    pthread_kill(a.id, 36); 
}
\end{lstlisting}
This displays the simple principal behind the \texttt{Exception} method of ATC.
However, there are many important considerations for an implementation of ATC
using this approach. 
\par\bigskip\noindent
First, as the abortable function may \texttt{throw} and \texttt{catch} its own
\texttt{Exceptions}, this may interfere with the cancellation. For example, if
a \texttt{catch (Exception ex)} statement were used within the function, an
attempt to cancel would not succeed. 
In order to prevent this, this projects implementation does not use 
\texttt{Exceptions}, but instead uses an \texttt{Error}. 
Conceptually, \texttt{Errors} and \texttt{Exceptions} are
similar, however, they are used differently within D: \texttt{Exceptions} are used to
manage the control of flow under \texttt{Exception}al circumstances, whereas
\texttt{Errors} are typically used for terminating the program and tracing faults. 
As a result, \texttt{Errors} are allowed to propagate through all 
\texttt{Exception} handlers. The only
limitation here is that the end user does not program the
abortable code to catch all \texttt{Errors}. As \texttt{Errors} generally occur 
when the program should be aborted, it is not advisable to do this. 
Inversely, to prevent the asynchronous cancellation from catching
\texttt{Errors} an \texttt{ATCInterrupt} class is defined as a subclass of
\texttt{Error}. 
\par\bigskip\noindent
Secondly, ATC sections may be nested inside each other. Using the above 
method, it would not be possible to exit two ATC sections through a single interrupt
to the outermost section: the \texttt{ATCInterrupt} would be caught and
handled in the inner ATC section, leaving the outer unaffected.
In order to allow nesting of Interruptible sections of code, the
\texttt{ATCInterrupt} thrown is therefore re-thrown if not caught by its 
corresponding \texttt{Interruptible} section. 
This is achieved by providing each \texttt{ATCInterrupt} with a notion of it's
owning \texttt{Interruptible} class. In the \texttt{finally} statement, this
value is checked, and the \texttt{ATCInterrupt} rethrown if the values do not
match. The implementation of this functionality is as follows: 
\begin{lstlisting}[basicstyle=\small]
private class ATCInterrupt : Error
{
    Interruptible owner;
    this(Interruptible own)
    {
        super(null, null);
        owner = own;
    }
}

class Interruptible
{
    private ATCInterrupt error; 
    private void function() fn; 

    this(void function() func)
    {
        error = new ATCInterrupt(this); 
        fn = func
    }

    private ATCInterrupt caughtInt;

    void start()
    {
        try
        {
            fn();
        }
        catch (ATCInterrupt ex)
        {
            if (ex.owner != this)
                caughtInt = ex;
        }
        finally
        {
            if (!(caughtInt is null))
                throw caughtInt;
        }
    }
}
\end{lstlisting}
This example enables both the use of \texttt{Exception}s within the ATC region and
enables ATC sections to be nested inside each other. 
As with the \texttt{Thread} based approach, it must be possible to defer interrupts
for the safety of the system. 
In Real-time Java, this is achieved through the use of runtime reflection to detect
whether the current function is capable of throwing an \texttt{Exception} or not.
However, as D does not a concept of run-time reflection, this approach cannot be
taken. 
Instead, there are two alternatives: automatically managing the deferral
status, or leaving deferral to the developer. 
\par\bigskip\noindent
In the first approach, the core runtime and standard libraries would have to be 
modified in order to give the language and understanding of ATC. Through doing 
this, it would be possible to define a function property, such as \texttt{@ATCDeferred},  
where it would not be possible to asynchronously interrupt. This follows Ada's
approach to abort deferred regions. 
This would be the preferred method of implementing ATC, as it would remove the
concern over the systems safety. However, it would require significant
reworking of the D runtime, and possibly compiler alterations.
As such, this method is outside of the scope of this
project. 
\par\bigskip\noindent
The alternative approach is to leave the safety of ATC to the developer by
providing methods to disable and re-enable deferral.
This can be achieved through the use of two boolean flags: one to keep track
of whether interrupts are deferred, and one to test whether an interrupt has
arrived while they have been deferred. 
However, as with the \texttt{Thread} approach, an extra method is then needed in 
order to guarantee fully safe execution during nested ATC sections. By keeping
track of each \texttt{Interruptible} section's parent, it is possible to defer
all interrupts, guaranteeing that no \texttt{Error} will be thrown. This is
achieved in the following manner: 
\begin{lstlisting}[basicstyle=\small]
class Interruptible
{
    ..
    void executeSafely(void delegate() fn) 
    {
        defer();
        scope(exit) restore();
        fn();
    }

    private bool previousDeferState;

    private void defer() @safe
    {
        if (! (parent is null) )
        {
            parent.defer;
        }
        previousDeferState = this.deferred;
        this.deferred = true;
    }

    private void restore() @safe
    {
        if (! (parent is null))
        {
            parent.restore;
        }
        this.deferred = previousDeferState;
    }
}
\end{lstlisting}
This can then be invoked in the same manner as the \texttt{Thread} based
implementation.
\par\bigskip\noindent
Furthermore a method of specifying a cleanup function is needed. This
is possible through the use of D's inbuilt \texttt{scope (exit)}:
this executes code specified on exiting the current scope, regardless of
whether this was through normal control flow or as a result of an
\texttt{Exception} or \texttt{Error}. 
\par\bigskip\noindent
Therefore, as highlighted, there are two implementations of Asynchronous
Transfer of Control in D: a two-thread model and a Signal/Exception based
approach. Both of these methods meet all the desired requirements of ATC: 
they can be used safely; allow cleanup; immediately transfer control; and
abstract any unnecessary complexity away from the developer. 
\par\bigskip\noindent
However, in some aspects the two approaches differ. 
Using the \texttt{Thread} based approach, a new \texttt{Thread} is
created for each invocation. This causes an increased overhead in creating an 
\texttt{Interruptible} section compared to the \texttt{Exception} model.
For cancellation, both
\texttt{pthread\textunderscore{}kill} and \texttt{pthread\textunderscore{}cancel}
are operating system calls that use signals in their implementation. It is
therefore expected that these approaches have a similar cost of cancellation.
\par\bigskip\noindent
However, the \texttt{Exception} based approach to ATC depends heavily on correct 
setup of the target \texttt{Thread's} stack when handling a signal. 
On different CPU architectures, it is possible that the signal handler may not 
allow propagation through the stack.
In order to provide a generalisable means of performing ATC, both of
these methods have therefore been implemented. 

\section{Requirement 9: Provision of Testing Facilities}
In order to verify the correct behaviour of the completed implementation, a set 
of tests have been provided. These serve two purposes: they enable verification
of the correct operation of the real-time library; and they improve the
maintainability of the system. 
D provides a built-in 
functionality for unit-testing: using the \texttt{unittest} flag, test code can 
be placed alongside function declarations. In addition, \texttt{assert} statements 
allow expected behaviour to be verified. 
This is shown in the following sample:
\begin{lstlisting}[basicstyle=\small]
int triple(int i) 
{
    return i*3; 
}
unittest 
{
    assert(triple(0) == 0); 
    assert(triple(-4) == -12); 
    assert(triple(10) == 30); 
    assert(12.triple == 36);
}
\end{lstlisting}
When using either of the two most popular compilers, LDC or DMD, a compiler
flag may be used to insert any \texttt{unittest} code into the beginning of the 
main function.
A main function may also be artificially created through a compiler flag:
\texttt{-main}. This allows the full testing of library code through a compiler 
invocation in the following manner: 
\begin{lstlisting}[basicstyle=\small]
ldc -main -unittest -run realtime.d
\end{lstlisting}
This inbuilt definition of \texttt{unittest} allows unit-testing to occur 
without the need for tools external to the core language, thus providing a convenient 
and practical testing method. 
